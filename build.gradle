plugins {
    id 'java'
    id 'de.fuerstenau.buildconfig' version '1.1.4'
}

version = '3.5'
group = 'cn.matrixaura.lepton'
archivesBaseName = 'Lepton'

def execute(String cmd) {
    try {
        def stdout = new ByteArrayOutputStream()

        exec {
            commandLine cmd.split(" ")
            standardOutput = stdout
        }

        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

buildConfig {
    buildConfigField "String", "HASH", "${execute("git rev-parse --short HEAD")}"
    buildConfigField "String", "BRANCH", "${execute("git branch").replace("* ", "")}"
    buildConfigField "String", "VERSION", "${project.version}"

    packageName "cn.matrixaura.lepton"
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

sourceCompatibility = targetCompatibility = '8'
compileJava {
    sourceCompatibility = targetCompatibility = '8'
    options.encoding 'UTF-8'
}

repositories {
    mavenCentral()
}

configurations {
    dependency
    implementation.extendsFrom dependency
}

dependencies {
    dependency 'org.ow2.asm:asm-all:5.2'
    dependency 'net.java.dev.jna:jna:5.13.0'
    dependency 'net.java.dev.jna:jna-platform:5.13.0'
    dependency 'net.sf.json-lib:json-lib:2.4:jdk15'
    dependency fileTree(dir: 'libs', includes: ['*.jar'])

    implementation files("${System.properties['java.home']}/../lib/tools.jar")
    implementation fileTree("mclibs/")
}

jar {
    from(configurations.dependency.collect { it.isDirectory() ? it : zipTree(it) })

    manifest.attributes(
            'Agent-Class': 'cn.matrixaura.lepton.inject.AgentMain',
            'Main-Class': 'cn.matrixaura.lepton.Main',
            'Can-Redefine-Classes': 'true'
    )
}
